use std::iter::once;
use gc::Gc;

use crate::heap::{Heap, Lifetime};

use crate::syntax::*;
use crate::syntax::Term::*;
use crate::syntax::ToplevelCmd::*;

use crate::token::*;

grammar<'input>(heap: &mut Heap, text: &'input str);

// This defines the interface with the lexer
extern {
  type Location = usize;
  type Error    = Error;
  enum Token<'input> {
    "VAR"        => Token::VAR(<&'input str>),
    "NUMBER"     => Token::NUMBER(<&'input str>),
    "CONST"      => Token::CONST(<&'input str>),
    "FROM"       => Token::FROM{..},
    "COMMA"      => Token::COMMA{..},
    "TRUE"       => Token::TRUE{..},
    "PERIOD"     => Token::PERIOD{..},
    "PIPE"       => Token::PIPE{..},
    "LPAREN"     => Token::LPAREN{..},
    "RPAREN"     => Token::RPAREN{..},
    "LBRACKET"   => Token::LBRACKET{..},
    "RBRACKET"   => Token::RBRACKET{..},
    "GOAL"       => Token::GOAL{..},
    "QUIT"       => Token::QUIT{..},
    "USE"        => Token::USE{..},
    "STRING"     => Token::STRING(<&'input str>),
  }
}
// Productions

// toplevel:
//   | EOF                      { [] }
//   | exprtop                  { $1 }
//   | cmdtop                   { $1 }
pub Toplevel: Vec<ToplevelCmd> = {
  Exprtop,
  Cmdtop
};

// exprtop:
//   | expr EOF                 { [$1] }
//   | expr toplevel            { $1 :: $2 }
Exprtop: Vec<ToplevelCmd> = {
  <Expr>                => vec![<>],
  <e:Expr> <t:Toplevel> => { once(e).chain(t.into_iter()).collect() }
};

// cmdtop:
//   | cmd EOF                  { [$1] }
//   | cmd toplevel             { $1 :: $2 }
Cmdtop: Vec<ToplevelCmd> = {
  <Cmd>                => vec![<>],
  <c:Cmd> <t:Toplevel> => { once(c).chain(t.into_iter()).collect() }
};

// cmd:
//   | USE STRING { Use $2 }
//   | QUIT       { Quit }
Cmd: ToplevelCmd = {
  "USE" <"STRING"> => Use(<>.to_string()),
  "QUIT"           => Quit
};

// expr:
//   | goal      { $1 }
//   | assertion { $1 }
Expr = {
  Goal,
  Assertion
};

// This is a clever little trick to make it so there is less duplication in the
// grammar.  When we hit a "Goal" production, we would like to use the ephemeral
// heap for parsing the expressions. To achieve this, all the productions
// rechable from "Goal" have both "Perm" and "Ephemeral" versions. We can use
// the macro feature of lalrpop to fold these back into a single definition.
// these two productions are here because we can use them to move the lifetime
// parameter from the lalrpop macro into the rust value level. See for example,
// "Atom" where it uses <l:Lifetime> at the start of each production.
// Perm/Ephemeral always match and always produce the corresponding value of
// type Lifetime. So this allows us to gain access to the parameter to the
// lalrpop macro.
Perm:      Lifetime = { => Lifetime::Perm      };
Ephemeral: Lifetime = { => Lifetime::Ephemeral };

// goal:
//   | GOAL clause PERIOD        { Goal $2 }
Goal: ToplevelCmd = {
  "GOAL" <Clause<Perm>> "PERIOD" => Goal(<>)
};

// assertion:
//   | atom PERIOD               { Assert ($1, []) }
//   | atom FROM clause PERIOD   { Assert ($1, $3) }
Assertion: ToplevelCmd = {
  <Atom<Perm>> "PERIOD"                           => Assert((<>, vec![])),
  <a:Atom<Perm>> "FROM" <c:Clause<Perm>> "PERIOD" => Assert((a,c))
};

// atom:
//   | CONST                     { ($1, []) }
//   | CONST LPAREN args RPAREN  { ($1, $3) }
Atom<Lifetime> : (Constant, Vec<Gc<Term>>) = {

  <l:Lifetime> <c:"CONST"> =>
      (heap.insert_string(c.to_string(), l), vec![]),

  <l:Lifetime> <c:"CONST"> "LPAREN" <a:Args<Lifetime>> "RPAREN" =>
      (heap.insert_string(c.to_string(), l), a)

};

// clause:
//   | TRUE                      { [] }
//   | atom                      { [$1] }
//   | atom COMMA clause         { $1 :: $3 }
Clause<Lifetime>: Vec<Atom> = {
  "TRUE"                                          => vec![],
  <Atom<Lifetime>>                                => vec![<>],
  <a:Atom<Lifetime>> "COMMA" <c:Clause<Lifetime>> => { once(a).chain(c.into_iter()).collect() }
};

// args:
//   | literal            { [$1] }
//   | literal COMMA args { $1 :: $3 }
Args<Lifetime>: Vec<Gc<Term>> = {
  <Literal<Lifetime>>                              => vec![<>],
  <l:Literal<Lifetime>> "COMMA" <a:Args<Lifetime>> => { once(l).chain(a.into_iter()).collect() }
};

// literal:
//   | CONST                    { Const $1 }
//   | VAR                      { Var ($1, 0) }
//   | NUMBER                   { convert to calls to succ }
//   | CONST LPAREN args RPAREN { App ($1, $3) }
Literal<Lifetime>: Gc<Term> = {
  <l:Lifetime> <c:"CONST">                              => {
    let c_str = heap.insert_string(c.to_string(), l);
    heap.insert_term( Const(c_str), l )
  },
  <l:Lifetime> <v:"VAR">                                => {
    let v_str = heap.insert_string(v.to_string(), l);
    heap.insert_term( Var((v_str, 0)), l )
  },
  <l:Lifetime> <n:"NUMBER">                             => {
    str_to_nat(heap, n, l)
  },
  <l:Lifetime> <c:"CONST"> "LPAREN" <a:Args<Lifetime>> "RPAREN" => {
    let app = heap.insert_string(c.to_string(), l);
    heap.insert_term( App(app, a), l )
  },
  ListLiteral<Lifetime> =>  <>
};

ListLiteral<Lifetime>: Gc<Term> = {
  <l:Lifetime> "LBRACKET" "RBRACKET"                  => vec_to_list(heap, vec![], l),
  <l:Lifetime> "LBRACKET" <a:Args<Lifetime>> "RBRACKET"         => {
    vec_to_list(heap, a, l)
  },
  <l:Lifetime> "LBRACKET" <head:Literal<Lifetime>> "PIPE" <tail:Literal<Lifetime>> "RBRACKET" => {
    // "cons" is a common string mentioned in the prelude so it can be hardcoded
    // to Perm.
    let cons = heap.insert_string("cons".to_string(), Lifetime::Perm);
    heap.insert_term(Term::App(cons, vec![head, tail]), l)
  }
};

